### Variables ###

# Define instance count of the DC/OS nodes

variable "workstation_instance_count" {
  description = "Number of workstation nodes to launch"
  default = 1
}

variable "master_instance_count" {
  description = "Number of master nodes to launch [1, 3, 5]"
  default = 3
}


variable "agent_instance_count" {
  description = "Number of agent nodes to launch [min 3]"
  default = 4
}

variable "public_agent_instance_count" {
  description = "Number of public agent nodes to launch [min 1]"
  default = 1
}

variable "access_key" {
  description = "AWS access key"
}

variable "secret_key" {
  description = "AWS secret key"
}

variable "region" {
  default = "us-west-2"
}

# AMIs for CentOS

variable "amis" {
  default = {
    eu-central-1 = "ami-9bf712f4"
    us-west-2 = "ami-d2c924b2"
    us-east-1 = "ami-6d1c2007"
  }
}

variable "subnet_range" {
  description = "Subnet IP range"
  default = "172.31.0.0/16"
}

variable "subnet_dns" {
  description = "Subnet DNS"
  default = "172.31.0.2"
}

variable "admin_ip" {
  default = "0.0.0.0/0"
}

variable "key_name" {
  description = "Name of existing AWS key pair to use"
}

variable "owner" {
  description = "AWS tag of the owner (e.g. Slack username)"
}

variable "expiration" {
  description = "AWS tag of the expiration time"
  default = "8hours"
}

### Outputs ###

output "workstation_public_ips" {
    value = "${join("\n", aws_instance.workstations.*.public_ip)}"
}

output "workstation_private_ips" {
    value = "${join("\n  - ", aws_instance.workstations.*.private_ip)}"
}

output "master_public_ips" {
    value = "${join("\n", aws_instance.masters.*.public_ip)}"
}

output "master_private_ips" {
    value = "${join("\n  - ", aws_instance.masters.*.private_ip)}"
}

output "agent_public_ips" {
    value = "${join("\n", aws_instance.agents.*.public_ip)}"
}

output "public_agent_public_ips" {
    value = "${join("\n", aws_instance.public_agents.*.public_ip)}"
}

output "dns" {
    value = "${var.subnet_dns}"
}

output "dns_search" {
    value = "${var.region}.compute.internal"
}

### Create Environment ###

# Specify the provider and access details
provider "aws" {
    access_key = "${var.access_key}"
    secret_key = "${var.secret_key}"
    region = "${var.region}"
}

# Create a VPC to launch our instances into
resource "aws_vpc" "dcos_vpc" {
  cidr_block = "${var.subnet_range}"
  enable_dns_support = true
  enable_dns_hostnames = true

}

# Create a subnet to launch our instances into
resource "aws_subnet" "dcos_subnet" {

  vpc_id = "${aws_vpc.dcos_vpc.id}"
  cidr_block = "${var.subnet_range}"
  map_public_ip_on_launch = true

}

# Create an internet gateway to give our subnet access to the outside world
resource "aws_internet_gateway" "default" {
  vpc_id = "${aws_vpc.dcos_vpc.id}"
}

# Grant the VPC internet access on its main route table
resource "aws_route" "internet_access" {
  route_table_id         = "${aws_vpc.dcos_vpc.main_route_table_id}"
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = "${aws_internet_gateway.default.id}"
}

# Define IAM role to create external volumes on AWS
resource "aws_iam_instance_profile" "agent_profile" {
  name = "${var.owner}-agent_profile"
  roles = ["${aws_iam_role.dcos_agent_role.name}"]

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_iam_role_policy" "agent_policy" {
    name = "${var.owner}-agent_policy"
    role = "${aws_iam_role.dcos_agent_role.id}"
    policy = <<EOF
{
    "Version": "2012-10-17",
    "Statement": [
        {
            "Action": [
                "ec2:CreateTags",
                "ec2:DescribeInstances",
                "ec2:CreateVolume",
                "ec2:DeleteVolume",
                "ec2:AttachVolume",
                "ec2:DetachVolume",
                "ec2:DescribeVolumes",
                "ec2:DescribeVolumeStatus",
                "ec2:DescribeVolumeAttribute",
                "ec2:CreateSnapshot",
                "ec2:CopySnapshot",
                "ec2:DeleteSnapshot",
                "ec2:DescribeSnapshots",
                "ec2:DescribeSnapshotAttribute"
            ],
            "Resource": "*",
            "Effect": "Allow"
        }
    ]
}
EOF
}

resource "aws_iam_role" "dcos_agent_role" {
    name = "${var.owner}-dcos_agent_role"
    assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "ec2.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}

# Default security group
resource "aws_security_group" "dcos_sg" {
  name = "${var.owner}-sg"
  description = "Allow all inbound traffic"
  vpc_id = "${aws_vpc.dcos_vpc.id}"

  ingress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["${var.subnet_range}"]
  }

  ingress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["${var.admin_ip}"]
  }

  egress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

}

# Create Workstations
resource "aws_instance" "workstations" {
  instance_type = "t2.micro"
  ami = "${lookup(var.amis, var.region)}"

  count = "${var.workstation_instance_count}"
  key_name = "${var.key_name}"
  subnet_id = "${aws_subnet.dcos_subnet.id}"
  vpc_security_group_ids = ["${aws_security_group.dcos_sg.id}"]
  associate_public_ip_address = true

  tags {
          Name = "${var.owner}-dcos-workstation"
          owner = "${var.owner}"
          expiration = "${var.expiration}"
      }

}

# Create Masters
resource "aws_instance" "masters" {
  instance_type = "m3.xlarge"
  ami = "${lookup(var.amis, var.region)}"

  count = "${var.master_instance_count}"
  key_name = "${var.key_name}"
  subnet_id = "${aws_subnet.dcos_subnet.id}"
  vpc_security_group_ids = ["${aws_security_group.dcos_sg.id}"]
  associate_public_ip_address = true

  tags {
          Name = "${var.owner}-dcos-master-${count.index}"
          owner = "${var.owner}"
          expiration = "${var.expiration}"
      }

#  root_block_device {
#    volume_size = 20
#  }

}

# Create Agents
resource "aws_instance" "agents" {
  instance_type = "m3.xlarge"
  ami = "${lookup(var.amis, var.region)}"

  count = "${var.agent_instance_count}"
  key_name = "${var.key_name}"
  subnet_id = "${aws_subnet.dcos_subnet.id}"
  vpc_security_group_ids = ["${aws_security_group.dcos_sg.id}"]
  associate_public_ip_address = true
  iam_instance_profile = "${aws_iam_instance_profile.agent_profile.name}"

  tags {
          Name = "${var.owner}-dcos-agent-${count.index}"
          owner = "${var.owner}"
          expiration = "${var.expiration}"
      }

#  root_block_device {
#    volume_size = 20
#  }

}

# Create Public Agents
resource "aws_instance" "public_agents" {
  instance_type = "m3.xlarge"
  ami = "${lookup(var.amis, var.region)}"

  count = "${var.public_agent_instance_count}"
  key_name = "${var.key_name}"
  subnet_id = "${aws_subnet.dcos_subnet.id}"
  vpc_security_group_ids = ["${aws_security_group.dcos_sg.id}"]
  associate_public_ip_address = true
  iam_instance_profile = "${aws_iam_instance_profile.agent_profile.name}"

  tags {
          Name = "${var.owner}-dcos-public_agent-${count.index}"
          owner = "${var.owner}"
          expiration = "${var.expiration}"
      }

#  root_block_device {
#    volume_size = 20
#  }

}
